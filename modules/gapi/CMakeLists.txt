# FIXME: Rework standalone build in more generic maner
# (Restructure directories, add common pass, etc)
if (NOT DEFINED OPENCV_INITIAL_PASS)
    include("cmake/standalone.cmake")
    return()
endif()

# FIXME: Remove CXX11 check after complete switch to OpenCV 4 branch
# (CI, bundle, workloads, etc)
if (NOT HAVE_CXX11 OR NOT TARGET ade)
  # can't build G-API because of the above reasons
  ocv_module_disable(gapi)
  return()
endif()

set(the_description "OpenCV G-API Core Module")
ocv_add_module(gapi opencv_imgproc)

file(GLOB gapi_ext_hdrs
    "${CMAKE_CURRENT_LIST_DIR}/include/opencv2/*.hpp"
    "${CMAKE_CURRENT_LIST_DIR}/include/opencv2/${name}/*.hpp"
    "${CMAKE_CURRENT_LIST_DIR}/include/opencv2/${name}/*.h"
    "${CMAKE_CURRENT_LIST_DIR}/include/opencv2/${name}/util/*.hpp"
    "${CMAKE_CURRENT_LIST_DIR}/include/opencv2/${name}/cpu/*.hpp"
    "${CMAKE_CURRENT_LIST_DIR}/include/opencv2/${name}/gpu/*.hpp"
    "${CMAKE_CURRENT_LIST_DIR}/include/opencv2/${name}/fluid/*.hpp"
    "${CMAKE_CURRENT_LIST_DIR}/include/opencv2/${name}/own/*.hpp"
    )

set(gapi_srcs
    # Front-end part
    src/api/gapi_priv.cpp
    src/api/gmat.cpp
    src/api/garray.cpp
    src/api/gscalar.cpp
    src/api/gkernel.cpp
    src/api/gbackend.cpp
    src/api/gproto.cpp
    src/api/gnode.cpp
    src/api/gcall.cpp
    src/api/gcomputation.cpp
    src/api/operators.cpp
    src/api/kernels_core.cpp
    src/api/kernels_imgproc.cpp

    # Compiler part
    src/compiler/gmodel.cpp
    src/compiler/gmodelbuilder.cpp
    src/compiler/gislandmodel.cpp
    src/compiler/gcompiler.cpp
    src/compiler/gcompiled.cpp
    src/compiler/passes/helpers.cpp
    src/compiler/passes/dump_dot.cpp
    src/compiler/passes/islands.cpp
    src/compiler/passes/meta.cpp
    src/compiler/passes/kernels.cpp
    src/compiler/passes/exec.cpp

    # Executor
    src/executor/gexecutor.cpp

    # CPU Backend (currently built-in)
    src/backends/cpu/gcpubackend.cpp
    src/backends/cpu/gcpukernel.cpp
    src/backends/cpu/gcpuimgproc.cpp
    src/backends/cpu/gcpucore.cpp

    # Fluid Backend (also built-in, FIXME:move away)
    src/backends/fluid/gfluidbuffer.cpp
    src/backends/fluid/gfluidbackend.cpp
    src/backends/fluid/gfluidimgproc.cpp
    src/backends/fluid/gfluidimgproc_func.dispatch.cpp
    src/backends/fluid/gfluidcore.cpp

    # GPU Backend (currently built-in)
    src/backends/gpu/ggpubackend.cpp
    src/backends/gpu/ggpukernel.cpp
    src/backends/gpu/ggpuimgproc.cpp
    src/backends/gpu/ggpucore.cpp

    # Compound
    src/backends/common/gcompoundbackend.cpp
    src/backends/common/gcompoundkernel.cpp
    )

ocv_add_dispatched_file(backends/fluid/gfluidimgproc_func SSE4_1 AVX2)

ocv_list_add_prefix(gapi_srcs "${CMAKE_CURRENT_LIST_DIR}/")

file(GLOB cl_kernels
     "${CMAKE_CURRENT_LIST_DIR}/src/backends/gpu/opencl/*.cl"
)
if(cl_kernels)
  set(OCL_NAME opencl_kernels_${name})
  add_custom_command(
    OUTPUT "${CMAKE_CURRENT_BINARY_DIR}/${OCL_NAME}.cpp"  # don't add .hpp file here to optimize build process
    COMMAND ${CMAKE_COMMAND} "-DMODULE_NAME=${name}" "-DCL_DIR=${CMAKE_CURRENT_LIST_DIR}/src/backends/gpu/opencl" "-DOUTPUT=${CMAKE_CURRENT_BINARY_DIR}/${OCL_NAME}.cpp" -P "${OpenCV_SOURCE_DIR}/cmake/cl2cpp.cmake"
    DEPENDS ${cl_kernels} "${OpenCV_SOURCE_DIR}/cmake/cl2cpp.cmake"
    COMMENT "Processing OpenCL kernels (${name})"
  )
  ocv_source_group("Src\\backends\\gpu\\opencl\\kernels" FILES ${cl_kernels})
  ocv_source_group("Src\\backends\\gpu\\opencl\\kernels\\autogenerated" FILES "${CMAKE_CURRENT_BINARY_DIR}/${OCL_NAME}.cpp" "${CMAKE_CURRENT_BINARY_DIR}/${OCL_NAME}.hpp")
  set_source_files_properties("${CMAKE_CURRENT_BINARY_DIR}/${OCL_NAME}.cpp" "${CMAKE_CURRENT_BINARY_DIR}/${OCL_NAME}.hpp"
      PROPERTIES GENERATED TRUE
  )
  list(APPEND gapi_srcs ${cl_kernels} "${CMAKE_CURRENT_BINARY_DIR}/${OCL_NAME}.cpp" "${CMAKE_CURRENT_BINARY_DIR}/${OCL_NAME}.hpp")
endif()

file(GLOB cl_kernels_test
     "${CMAKE_CURRENT_LIST_DIR}/test/opencl/*.cl"
)
if(cl_kernels_test)
  set(OCL_NAME_TEST opencl_kernels_test_${name})
  add_custom_command(
    OUTPUT "${CMAKE_CURRENT_LIST_DIR}/test/${OCL_NAME_TEST}.cpp"  # don't add .hpp file here to optimize build process
    COMMAND ${CMAKE_COMMAND} "-DMODULE_NAME=${name}" "-DCL_DIR=${CMAKE_CURRENT_LIST_DIR}/test/opencl" "-DOUTPUT=${CMAKE_CURRENT_LIST_DIR}/test/${OCL_NAME_TEST}.cpp" -P "${OpenCV_SOURCE_DIR}/cmake/cl2cpp.cmake"
    DEPENDS ${cl_kernels_test} "${OpenCV_SOURCE_DIR}/cmake/cl2cpp.cmake"
    COMMENT "Processing OpenCL test kernels (${name})"
  )
  ocv_source_group("Test\\opencl\\kernels" FILES ${cl_kernels_test})
  ocv_source_group("Test\\opencl\\kernels\\autogenerated" FILES "${CMAKE_CURRENT_LIST_DIR}/test/${OCL_NAME_TEST}.cpp" "${CMAKE_CURRENT_LIST_DIR}/test/${OCL_NAME_TEST}.hpp")
  set_source_files_properties("${CMAKE_CURRENT_LIST_DIR}/test/${OCL_NAME_TEST}.cpp" "${CMAKE_CURRENT_LIST_DIR}/test/${OCL_NAME_TEST}.hpp"
      PROPERTIES GENERATED TRUE
  )
  #list(APPEND gapi_srcs ${cl_kernels_test} "${CMAKE_CURRENT_LIST_DIR}/test/${OCL_NAME_TEST}.cpp" "${CMAKE_CURRENT_LIST_DIR}/test/${OCL_NAME_TEST}.hpp")
endif()


# For IDE users
ocv_source_group("Src"     FILES ${gapi_srcs})
ocv_source_group("Include" FILES ${gapi_ext_hdrs})

ocv_set_module_sources(HEADERS ${gapi_ext_hdrs} SOURCES ${gapi_srcs})
ocv_module_include_directories("${CMAKE_CURRENT_LIST_DIR}/src")

# Note `ade` is not a module name but link dependency for ${the_module}
# (which is opencv_gapi)
ocv_create_module(ade)

ocv_add_accuracy_tests()
# FIXME: test binary is linked with ADE directly since ADE symbols
# are not exported from libopencv_gapi.so in any form - thus
# there're two copies of ADE code in memory when tests run (!)
# src/ is specified to include dirs for INTERNAL tests only.
if(TARGET opencv_test_gapi)
  target_include_directories(opencv_test_gapi PRIVATE "${CMAKE_CURRENT_LIST_DIR}/src")
  target_link_libraries(opencv_test_gapi PRIVATE ade)
endif()

ocv_add_perf_tests()
ocv_add_samples()
