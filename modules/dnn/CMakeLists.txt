if(WINRT)
  ocv_module_disable(dnn)
endif()

if(DEFINED BUILD_opencv_dnn AND NOT BUILD_opencv_dnn)
  return()
endif()

# One and only way if dnn module will be disabled - failed to link to system protobuf.
if(DEFINED BUILD_PROTOBUF AND NOT BUILD_PROTOBUF)
  include(${OpenCV_SOURCE_DIR}/cmake/OpenCVFindLibProtobuf.cmake)
  if(NOT Protobuf_FOUND)
    ocv_module_disable(opencv_dnn)
  endif()
endif()

set(the_description "Deep neural network module. It allows to load models from different frameworks and to make forward pass")

ocv_add_dispatched_file("layers/layers_common" AVX AVX2)

ocv_add_module(dnn REQUIRED opencv_core opencv_imgproc
               OPTIONAL opencv_protobuf_parser
               WRAP python matlab java)

ocv_option(${the_module}_USE_PROTOBUF "Force use of Protobuf library instead OpenCV's protobuf parser" OFF)

set(USE_PROTOBUF ON)
if(NOT ${the_module}_USE_PROTOBUF AND HAVE_opencv_protobuf_parser)
  set(USE_PROTOBUF OFF)
endif()

if(USE_PROTOBUF)
  if((NOT DEFINED BUILD_PROTOBUF) OR
      (DEFINED BUILD_PROTOBUF AND BUILD_PROTOBUF))
    include(${OpenCV_SOURCE_DIR}/cmake/OpenCVFindLibProtobuf.cmake)
  endif()
endif()

ocv_warnings_disable(CMAKE_CXX_FLAGS -Wno-shadow -Wno-parentheses -Wmaybe-uninitialized -Wsign-promo
                                     -Wmissing-declarations -Wmissing-prototypes
)
ocv_warnings_disable(CMAKE_CXX_FLAGS /wd4701 /wd4100)

include_directories(${CMAKE_CURRENT_SOURCE_DIR}/src/ocl4dnn/include ${OPENCL_INCLUDE_DIRS})

if(MSVC)
  add_definitions( -D_CRT_SECURE_NO_WARNINGS=1 )
  ocv_warnings_disable(CMAKE_CXX_FLAGS /wd4244 /wd4267 /wd4018 /wd4355 /wd4800 /wd4251 /wd4996 /wd4146
                                       /wd4305 /wd4127 /wd4100 /wd4512 /wd4125 /wd4389 /wd4510 /wd4610
                                       /wd4702 /wd4456 /wd4457 /wd4065 /wd4310 /wd4661 /wd4506
  )
else()
  ocv_warnings_disable(CMAKE_CXX_FLAGS -Wno-deprecated -Wmissing-prototypes -Wmissing-declarations -Wshadow
                                       -Wunused-parameter -Wunused-local-typedefs -Wsign-compare -Wsign-promo
                                       -Wundef -Wtautological-undefined-compare -Wignored-qualifiers -Wextra
                                       -Wunused-function -Wunused-const-variable -Wdeprecated-declarations
  )
endif()

if(APPLE_FRAMEWORK)
  ocv_warnings_disable(CMAKE_CXX_FLAGS -Wshorten-64-to-32)
endif()

if(ANDROID)
  add_definitions(-DDISABLE_POSIX_MEMALIGN -DTH_DISABLE_HEAP_TRACKING)
endif()

if(USE_PROTOBUF)
  #supress warnings in autogenerated caffe.pb.* files
  add_definitions(-DHAVE_PROTOBUF=1)
  ocv_warnings_disable(CMAKE_CXX_FLAGS
      -Wunused-parameter -Wundef -Wignored-qualifiers -Wno-enum-compare
      -Wdeprecated-declarations
      /wd4125 /wd4267 /wd4127 /wd4244 /wd4512 /wd4702
      /wd4456 /wd4510 /wd4610 /wd4800
      -wd858 -wd2196
  )

  if(PROTOBUF_UPDATE_FILES)
    file(GLOB proto_files ${CMAKE_CURRENT_SOURCE_DIR}/src/tensorflow/*.proto)
    list(APPEND proto_files ${CMAKE_CURRENT_SOURCE_DIR}/src/caffe/opencv-caffe.proto)
    set(PROTOBUF_GENERATE_CPP_APPEND_PATH ON) # required for tensorflow
    PROTOBUF_GENERATE_CPP(Protobuf_HDRS Protobuf_SRCS ${proto_files})
  else()
    file(GLOB fw_srcs ${CMAKE_CURRENT_SOURCE_DIR}/misc/tensorflow/*.cc)
    file(GLOB fw_hdrs ${CMAKE_CURRENT_SOURCE_DIR}/misc/tensorflow/*.h)
    list(APPEND fw_srcs ${CMAKE_CURRENT_SOURCE_DIR}/misc/caffe/opencv-caffe.pb.cc)
    list(APPEND fw_hdrs ${CMAKE_CURRENT_SOURCE_DIR}/misc/caffe/opencv-caffe.pb.h)
    list(APPEND Protobuf_SRCS ${fw_srcs})
    list(APPEND Protobuf_HDRS ${fw_hdrs})
    list(APPEND Protobuf_INCLUDE_DIRS ${CMAKE_CURRENT_SOURCE_DIR}/misc/caffe)
    list(APPEND Protobuf_INCLUDE_DIRS ${CMAKE_CURRENT_SOURCE_DIR}/misc/tensorflow)
  endif()
else()
  include("${CMAKE_CURRENT_SOURCE_DIR}/cmake/binary_to_header.cmake")
  binary_to_header("${CMAKE_CURRENT_SOURCE_DIR}/misc/caffe/opencv-caffe_proto.pbtxt"
                   "${OPENCV_MODULE_${the_module}_BINARY_DIR}/opencv-caffe_proto.hpp")
  binary_to_header("${CMAKE_CURRENT_SOURCE_DIR}/misc/tensorflow/graph_proto.pbtxt"
                   "${OPENCV_MODULE_${the_module}_BINARY_DIR}/graph_proto.hpp")

  ocv_module_include_directories(include)

  ocv_glob_module_sources(${CBLAS_H_PROXY_PATH})
  ocv_create_module(${LAPACK_LIBRARIES})
endif()

ocv_add_samples()
ocv_add_accuracy_tests()
ocv_add_perf_tests()

ocv_option(${the_module}_PERF_CAFFE "Add performance tests of Caffe framework" OFF)
ocv_option(${the_module}_PERF_CLCAFFE "Add performance tests of clCaffe framework" OFF)
if(BUILD_PERF_TESTS)
  if(${the_module}_PERF_CAFFE)
    find_package(Caffe QUIET)
    if(Caffe_FOUND)
      add_definitions(-DHAVE_CAFFE=1)
      ocv_target_link_libraries(opencv_perf_dnn caffe)
    endif()
  elseif(${the_module}_PERF_CLCAFFE)
    find_package(Caffe QUIET)
    if(Caffe_FOUND)
      add_definitions(-DHAVE_CLCAFFE=1)
      ocv_target_link_libraries(opencv_perf_dnn caffe)
    endif()
  endif()
endif()

# ----------------------------------------------------------------------------
# Torch7 importer of blobs and models, produced by Torch.nn module
# ----------------------------------------------------------------------------
OCV_OPTION(${the_module}_BUILD_TORCH_IMPORTER "Build Torch model importer" ON)
if(${the_module}_BUILD_TORCH_IMPORTER)
    message(STATUS "Torch importer has been enabled. To run the tests you have to install Torch "
      "('th' executable should be available) "
      "and generate testdata using opencv_extra/testdata/dnn/generate_torch_models.py script.")
    add_definitions(-DENABLE_TORCH_IMPORTER=1)
    ocv_warnings_disable(CMAKE_CXX_FLAGS /wd4702 /wd4127 /wd4267) #supress warnings in original torch files
endif()
